plugins {
    id 'java'
}

group = 'dev.1brc'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

sourceSets {
    jmh {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
        }
    }
}

dependencies {
    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.37'
    jmhImplementation 'org.openjdk.jmh:jmh-core:1.37'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.1'
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['--enable-preview', '--add-modules', 'jdk.incubator.vector']
}

test {
    jvmArgs += ['--enable-preview', '--add-modules', 'jdk.incubator.vector']
    useJUnitPlatform()
}

task zip(type: Zip, dependsOn: 'build') {
    from './'
    include '*.sh'
    include 'data/*'
    include 'build/libs/*'
    archiveFileName = 'benchmark.zip'
    destinationDirectory = file('build/archives/')
}